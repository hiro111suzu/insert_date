//. 初期設定

$reg_date = "20[0-9][0-9].[0-1][0-9].[0-3][0-9](日|)";
$sep_y = "年"; $sep_m = "月"; $sep_d = "日";
#use_original = true;
$ahk_path = currentmacrodirectory + "\\insert_date.exe";

/*
この上を編集し、設定を行う

# $reg_date: 日付の正規表現パターン
例:
$reg_date = "[12][0-9]{3}-[0-1][0-9]-[0-3][0-9]";

# $sep_y: 出力する日付の書式

2022年01月01日 形式
$sep_y = "年"; $sep_m = "月"; $sep_d = "日"

2022-01-01 形式
$sep_y = "-"; $sep_m = "-"; $sep_d = "";

2022/01/01 形式
$sep_y = "/"; $sep_m = "/"; $sep_d = "";

2022.01.01 形式
//$sep_y = "."; $sep_m = "."; $sep_d = "";

# #use_original: もとの書式を使う
#use_original = true;

# AutoHotkeyの実行パス
AutoHotkeyをインストールしているなら、.exeファイルよりも.ahkファイルを実行したほうが速いし、改造もしやすい

例:
$ahk_path = @"C:\Program Files\AutoHotkey\AutoHotkey.exe "
	 + currentmacrodirectory + "\\insert_date.ahk";
*/

$date_prefix = "<monthcal$%&>";

//.. 情報収集
#init_column = column;
#init_lineno = lineno;
#selecting = selecting;
$date = "today";
setcompatiblemode 0x00020000; // 終了後、検索条件を戻すモード
begingroupundo;

//. 日付取得
if ( selecting ) {
	call get_date seltopx, seltopy, selendx, selendy;
} else {
	golinetop;
	searchdown2 $reg_date, regular;
	if ( result && lineno == #init_lineno ) {
		call get_date, foundtopx, foundtopy, foundendx, foundendy;
	} else {
		moveto2 #init_column, #init_lineno;
		#xpixel = xpixel;
		#ypixel = ypixel;
	}
}

if ( selecting ) {
	#selecting = 1;
	if ( #use_original ) {
		$sep_y = cmumidstr( $date, 4, 1 );
		$sep_m = cmumidstr( $date, 7, 1 );
		$sep_d = cmumidstr( $date, 10, 1 );
	}
	$date = cmumidstr( $date, 0, 4 )
		+ cmumidstr( $date, 5, 2 )
		+ cmumidstr( $date, 8, 2 )
	;
}
disabledraw;

//. ahk 実行
runex $ahk_path
	 + " " + $date
	 + " " + str( #xpixel )
	 + " " + str( #ypixel + fontsize + 2 )
    , 1     //sync   0:async, 1:sync
    , 0, "" //stdin  0:none, 1:auto, 2:<file, 3:(reserved),
            //       4:current content, 5:selection
    , 6, "" //stdout 0:none, 1:auto, 2:>file 3:>>file, 4:new window,
            //       5:insert, 6:replace, 7:>output pane, 8:>>output pane
    , 0, "" //stderr 0:none, 1:auto or >>stdout, 2-8:same as stdout's param
    , 0, "" //folder 0:none, 1:current, 2:specify 3:(reserved), 4:exe's
    , 1     //show   0:auto, 1:show, 2:hide, 3-13:ShellExecute()'s SW_*
    , 1     //draw   0:draw, 1:no draw when stdout redirected
    , 0     //encode 0:ansi, 2:utf-16, 6:utf-8
    , 0     //extended flags
;

//. 手直し
searchup $date_prefix;
if ( ( !result ) || lineno != #init_lineno ) { //- 受け取らなかった、
	moveto2 #init_column, #init_lineno;
	if ( #selecting ) undo; //- 選択文字列が消えているので戻す
	goto end;
}
beginsel;
moveto foundendx, foundendy;
endsel;
delete; //- prefixを削除
$date = gettext2( column, lineno, column + 8, lineno );
beginsel; right 8; endsel;
insert ""
	+ midstr( $date, 0, 4 ) + $sep_y
	+ midstr( $date, 4, 2 ) + $sep_m
	+ midstr( $date, 6, 2 ) + $sep_d
;

//. end
end:
enabledraw;
endgroupundo;
endmacro;

//. サブルーチン
get_date:
	$date = gettext( ##1, ##2, ##3, ##4 );
	moveto  ##1, ##2;
	#xpixel = xpixel;
	#ypixel = ypixel;
	beginsel; 
	moveto  ##3, ##4;
	endsel;
return;

