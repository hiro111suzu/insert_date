//. 設定

$reg_date = "20[0-9][0-9].[0-1][0-9].[0-3][0-9](日|)(\\([月火水木金土]\\)|)";
$date_template = "%y%%年%m月%d日(%w)";
$ahk_path = currentmacrodirectory + "\\insert_date.exe";

/*
この上を編集し、設定を行う

# $reg_date: 日付の正規表現パターン
例:
$reg_date = "[12][0-9]{3}/[0-1][0-9]/[0-3][0-9]";
$reg_date = "[12][0-9]{3}-[0-1][0-9]-[0-3][0-9] ?[月火水木金土]?";

# $sep_y: 出力する日付の書式

2022年01月01日(土) 形式
$date_template = "%y%%年%m月%d日(%w)";

2022-01-01 形式
$date_template = "%y%%-%m-%d";

2022/01/01 土 形式
$date_template = "%y%%/%m/%d %w";

# AutoHotkeyの実行パス
AutoHotkeyをインストールしているなら、.exeファイルよりも.ahkファイルを実行したほうが速いし、改造もしやすい
例:
$ahk_path = @"C:\Program Files\AutoHotkey\AutoHotkey.exe "
	 + currentmacrodirectory + "\\insert_date.ahk";
*/

$date_prefix = "<monthcal$%&>";

//.. 情報収集
#init_column = column;
#init_lineno = lineno;
#selecting = selecting;
$date = "today";
setcompatiblemode 0x00020000; // 終了後、検索条件を戻すモード
begingroupundo;

//. 日付取得
if ( selecting ) {
	call get_date seltopx, seltopy, selendx, selendy;
} else {
	golinetop;
	selectline;
	searchdown2 $reg_date, regular, inselect;
	if ( result && lineno == #init_lineno ) {
		call get_date, foundtopx, foundtopy, foundendx, foundendy;
	} else {
		escape;
		moveto2 #init_column, #init_lineno;
		#xpixel = xpixel;
		#ypixel = ypixel;
	}
}
if ( selecting ) {
	#selecting = 1;
	$date = cmumidstr( $date, cmustrstr( $date_template, "%y%%" ), 4 )
		  + cmumidstr( $date, cmustrstr( $date_template, "%m" ),   2 )
		  + cmumidstr( $date, cmustrstr( $date_template, "%d" ),   2 )
	;
}
disabledraw;

//. ahk 実行
runex $ahk_path
	 + " " + $date
	 + " " + str( #xpixel )
	 + " " + str( #ypixel + fontsize + 2 )
    , 1     //sync   0:async, 1:sync
    , 0, "" //stdin  0:none, 1:auto, 2:<file, 3:(reserved),
            //       4:current content, 5:selection
    , 6, "" //stdout 0:none, 1:auto, 2:>file 3:>>file, 4:new window,
            //       5:insert, 6:replace, 7:>output pane, 8:>>output pane
    , 0, "" //stderr 0:none, 1:auto or >>stdout, 2-8:same as stdout's param
    , 0, "" //folder 0:none, 1:current, 2:specify 3:(reserved), 4:exe's
    , 1     //show   0:auto, 1:show, 2:hide, 3-13:ShellExecute()'s SW_*
    , 1     //draw   0:draw, 1:no draw when stdout redirected
    , 0     //encode 0:ansi, 2:utf-16, 6:utf-8
    , 0     //extended flags
;

//. 手直し
searchup $date_prefix;
if ( ( !result ) || lineno != #init_lineno ) {
	//- キャンセル（受け取らなかった）
	moveto2 #init_column, #init_lineno;
	if ( #selecting ) undo; //- 選択文字列が消えているので戻す
	goto end;
}
beginsel;
moveto foundendx, foundendy;
endsel;
delete; //- prefixを削除
$date = gettext2( column, lineno, column + 8, lineno );
beginsel; right 8; endsel;

//.. JSパート
js{(function(){
	var	d = hidemaru.getVar( '$date' ) ,
		dy = d.substring( 0, 4 ) ,
		dm = d.substring( 4, 6 ) ,
		dd = d.substring( 6, 8 ) ,
		dw = [ '日', '月', '火', '水', '木', '金', '土' ][ new Date( dy, dm-1, dd ).getDay() ]
//		dw = [ 'Sun', 'Mon', 'Tue', 'Wed', 'Thu', 'Fri', 'Sat' ]
//			[ new Date( dy, dm-1, dd ).getDay() ]
	;
	insert( hidemaru.getVar( '$date_template' )
		.replace( '%y%%', dy )
		.replace( '%m', dm )
		.replace( '%d', dd )
		.replace( '%w', dw )
	);
}());}


//. end
end:
enabledraw;
endgroupundo;
endmacro;

//. サブルーチン
get_date:
	$date = gettext( ##1, ##2, ##3, ##4 );
	moveto  ##1, ##2;
	#xpixel = xpixel;
	#ypixel = ypixel;
	beginsel; 
	moveto  ##3, ##4;
	endsel;
return;

